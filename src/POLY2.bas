REM POLY2
HIMEM=&2C00
N%=RND(-TIME)
PROCinit
REPEAT
PROCscreen
PROCstart
PROCboat
IF(lev%MOD2)=1PROCfish ELSE PROCshark
PROCbags
PROCstatus
PROCairbar
TIME=0
REPEAT
PROCdiver
PROCair
IF(lev%MOD2)=1PROCmovefish ELSE PROCmoveshark
PROCmovesealife
UNTILBAG%=0 OR DEAD%=0 OR O%<912
IFBAG%=0 PROCgreat:lev%=lev%+1
IFDEAD%=0 PROCover
IFO%<912 PROCover
UNTIL0
DEFPROCinit
LOCAL a%
FX%=&AB8:FY%=&AC0:FH%=&AC8:FV%=&AD0:F%=&AD8
BX%=&AE0:BY%=&AE8:B%=&AF0
DIM sky%(3):lev%=1
FORa%=0TO3:READ sky%(a%):NEXT
DATA 134,133,143,131
ENDPROC
DEFPROCscreen
VDU4,12,19,0,4;0;19,15,0;0;
VDU28,0,4,19,0,17,sky%((lev%-1)MOD4),12,5
IF(lev%MOD4)=1:PROCcirc(40,980,40,3):PROCmt(864,200,64,2):PROCmt(980,200,64,2)
IF(lev%MOD4)=2:PROCcirc(40,860,40,1):PROCmt(864,180,32,15):PROCmt(980,200,64,15)
IF(lev%MOD4)=3:PROCcirc(100,920,40,7):PROCcirc(120,920,40,15):PROCmt(864,180,32,7):PROCmt(980,200,64,7)
IF(lev%MOD4)=0:PROCcirc(40,860,40,5):PROCmt(864,120,32,1):PROCmt(980,200,64,1)
VDU4,26,28,0,31,19,27,17,143,12,26,28,0,28,19,27,17,132,12,5
FORT%=242TO247:VDU23,T%
FORI%=1TO8:VDURND(255)
NEXT,
FORY%=127TO160STEP32
FORX%=0TO1279STEP64 
MOVE X%,Y%
VDU18,0,3,RND(6)+241
NEXT,
ENDPROC
DEFPROCstart
FORG=0TO9
MOVEFNgrx,224:VDU18,0,2,225,10,8,224
NEXT
FORG=0TO9
MOVEFNgrx,224:VDU18,0,5,32,10,8,227
NEXT
X%=FNrx:Y%=FNry:Z%=1
PROCwrsprite(Z%,X%,Y%)
BW%=7:BH%=7:V%=2:H%=1
BAG%=8:DEAD%=8
O%=1224:OD%=200
SF%=1:DF%=1
IF(lev%MOD2)=1cr%=228:co%=1:ELSE:cr%=229:co%=5
crx%=FNgrx:VDU5,18,3,co%:MOVEcrx%,192:VDUcr%
ENDPROC
DEFPROCboat
VDU5,18,0,15:MOVE640,892:PRINT"\###/"
ENDPROC
DEFPROCmovesealife
ocx%=crx%
crx%=crx%+6:IFcrx%>1260crx%=0
IFcrx%<>ocx%:VDU5,18,3,co%:MOVEocx%,192:VDUcr%:MOVEcrx%,192:VDUcr%
ENDPROC
DEFPROCshark
SX%=FNrx:SY%=FNry:SD%=4:IFSX%<X% SD%=5
PROCwrsprite(SD%,SX%,SY%)
ENDPROC
DEFPROCfish
FORA=0TO7
FX%?A=FNrx:FY%?A=FNry:FH%?A=3:F%?A=3
IF A>3 THEN FH%?A=1:F%?A=2
FV%?A=8
PROCwrsprite(F%?A,FX%?A,FY%?A)
NEXT
E%=0:f%=-4
ENDPROC
DEFPROCbags
IF(lev%MOD4)=1:VDU23,251,8,24,56,126,255,62,28,8
IF(lev%MOD4)=2:VDU23,251,120,252,255,255,255,252,120,0
IF(lev%MOD4)=3:VDU23,251,102,153,153,126,126,153,153,102
IF(lev%MOD4)=0:VDU23,251,66,66,126,126,126,126,126,126
VDU5,18,3,1
FORA=0TO7
BX%?A=(RND(1100)+80)/8
BY%?A=(RND(600)+200)/4
MOVE BX%?A*8,BY%?A*4
VDU 251
NEXT
ENDPROC
DEFPROCairbar
VDU5:GCOL0,14:MOVE920,40:MOVE1216,40:PLOT85,920,52:PLOT85,1216,52
GCOL0,7:MOVE912,36:DRAW1224,36:DRAW1224,56:DRAW912,56:DRAW912,36
ENDPROC
DEFPROCair
IFTIME>OD%:O%=O%-8:SOUND0,4,6,3:IFO%>912 GCOL0,15:MOVEO%,52:DRAWO%,40:TIME=0
IFO%>1000 VDU19,14,6;0; ELSE VDU19,14,9;0;
ENDPROC
DEFPROCmoveshark
OSX%=SX%:OSY%=SY%:OSD%=SD%
IFSX%<X%ANDSX%<66 THEN SX%=SX%+SF%:SD%=5
IFSX%>X%ANDSX%>0 THEN SX%=SX%-SF%:SD%=4
IFSY%<Y%ANDSY%<200 THEN SY%=SY%+SF%
IFSY%>Y%ANDSY%>64 THEN SY%=SY%-SF%
IFSX%<>OSX% OR SY%<>OSY% PROCwrsprite(OSD%,OSX%,OSY%):PROCwrsprite(SD%,SX%,SY%)
safe%=SX%<X% OR SY%>Y% OR SX%>X%+12 OR SY%<Y%-12
IF NOT safe% PROChurt
ENDPROC
DEFPROCmovefish
IF FH%?E%=2 AND FY%?E%=0 THEN E%=(E%+1) MOD 8:ENDPROC
PROCwrsprite(F%?E%,FX%?E%,FY%?E%)
FX%?E%=FX%?E%+FH%?E%-2
FY%?E%=FY%?E%+FV%?E%-6
IF FX%?E%<4 THEN FH%?E%=3:F%?E%=3
IF FX%?E%>75 THEN FH%?E%=1:F%?E%=2
IF FY%?E%<64 AND FH%?E%<>2 THEN FV%?E%=8 ELSE IF FY%?E%<48 AND FH%?E%=2 THEN FY%?E%=48
IF FY%?E%>200 AND FH%?E%<>2 THEN FV%?E%=4
PROCwrsprite(F%?E%,FX%?E%,FY%?E%)
IF FH%?E%<>0 PROCfishcheck((FX%?E%+3*(F%?E%-2))*2,FY%?E%-5,E%)
E%=(E%+1) MOD 8
ENDPROC
DEFPROCdiver
A%=X%:B%=Y%:C%=Z%
IFINKEY-67ANDX%<68:X%=X%+DF%:Z%=1
IFINKEY-98ANDX%>0:X%=X%-DF%:Z%=0
IFINKEY-73ANDY%<224:Y%=Y%+(DF%*2)
IFINKEY-105ANDY%>54:Y%=Y%-(DF%*2)
IFINKEY-74AND(X%<>A% OR Y%<>B%):DF%=3:OD%=100:PROCair:ELSE:DF%=1:OD%=200
PROCwrsprite(C%,A%,B%)
PROCwrsprite(Z%,X%,Y%)
CX%=X%+6:CY%=Y%-14
PROCcheck(CX%*2,CY%)
IF FNrdscr(CX%,CY%)=5 OR FNrdscr(CX%+1,CY%)=5 OR FNrdscr(CX%+2,CY%)=5 OR FNrdscr(CX%+3,CY%)=5 OD%=50:SF%=2:PROChurt ELSE OD%=200
ENDPROC
DEFPROChurt
OD%=200:PROCair
SF%=2:SOUND1,-5,255,1
GCOL3,1:FORz=1TO4:PLOT69,(X%*16)+RND(128),(Y%*4)+RND(128):NEXT
ENDPROC
DEFPROCcheck(X%,Y%)
CALL Q%
C%=?&AB2
IF C%<8 THEN BAG%=BAG%-1:SOUND1,1,99,1:VDU18,3,1:MOVEBX%?C%*8,BY%?C%*4:VDU251:SOUND0,-10,150,1:BX%?C%=255:BY%?C%=255
PROCstatus
ENDPROC
DEFPROCfishcheck(X%,Y%,E%)
CALL Q%
C%=?&AB2
IF C%<8 THEN PROCdeadfish(E%):VDU18,3,1:MOVEBX%?C%*8,BY%?C%*4:VDU251:SOUND0,-10,150,1:BX%?C%=255:BY%?C%=255
ENDPROC
DEFPROCdeadfish(E%)
BAG%=BAG%-1
DEAD%=DEAD%-1
SOUND0,-10,50,10
FH%?E%=2:FV%?E%=0
PROCstatus
ENDPROC
DEFPROCstatus
VDU26,4,17,143,17,1,31,1,30,251,32,17,7:PRINT;BAG%
VDU31,6,30,17,6,241,32,17,7:PRINT;DEAD%
VDU31,11,30:PRINT"Air":VDU17,128,5
ENDPROC
DEFPROCgreat
VDU4,31,5,2:PRINT"NICE ONE!"
TIME=0:REPEATUNTILTIME>300:TIME=0:VDU5
ENDPROC
DEFPROCover
VDU4,31,5,2
IFBAG%<DEAD%PRINT"NICE!" ELSE PRINT"POOR!"
VDU31,2,3
PRINT"Spacebar to play"
*FX15
REPEATUNTILGET=32
lev%=1:ENDPROC
DEFPROCcirc(h%,v%,r%,c%)
VDU24,0;864;1279;1023;
VDU29,h%;v%;:GCOL0,c%
x%=r%:y%=0:z%=x%/2
REPEAT
MOVEx%,y%:PLOT5,-x%,y%
MOVEy%,x%:PLOT5,-y%,x%
MOVEx%,-y%:PLOT5,-x%,-y%
MOVEy%,-x%:PLOT5,-y%,-x%
y%=y%+1:z%=z%-y%
IF z%<0 THEN z%=z%+x%:x%=x%-1
UNTIL x%<y%
ENDPROC
DEFPROCmt(X%,LE%,HE%,CO%)
VDU29,X%;864;
MOVE0,0:L=LE%/PI:OX%=-32:OY%=0
FORx%=0TOLE%STEP64
y%=40+RND(40)+HE%*SIN(x%/L)
GCOL0,CO%:MOVEx%,y%:MOVEx%,0
PLOT85,OX%,OY%:PLOT85,OX%,0
GCOL0,15:MOVEOX%,OY%+4:DRAWx%,y%+4
OX%=x%:OY%=y%
NEXT
DRAWx%+16,4
GCOL0,CO%:MOVEOX%,0:MOVEOX%,OY%
PLOT85,x%+16,0
ENDPROC
DEFPROCwrsprite(A%,X%,Y%):CALLW%:ENDPROC
DEFFNrx:=RND(72)+4
DEFFNry:=RND(122)+90
DEFFNrdscr(X%,Y%):CALLR%:addr%=!&70 AND &FFFF:=?addr%
DEFFNgrx:=(RND(32)+2)*32
DEFFNgry:=(RND(33)+18)*16
